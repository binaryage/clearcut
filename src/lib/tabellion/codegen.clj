(ns tabellion.codegen
  "Helpers for generating code for core macros.
  The generated code greatly depends on effective tabellion configuration (see defaults.clj).
  e.g. diagnostics, debugging and other settings."
  (:refer-clojure :exclude [gensym])
  (:require [tabellion.config :as config]
            [tabellion.helpers :refer [gensym]]
            [tabellion.compiler :as compiler]
            [tabellion.debug :refer [log debug-assert]]
            [tabellion.constants :as constants]))

; -- helper code generators -------------------------------------------------------------------------------------------------

(defn gen-reported-message [msg]
  msg)

(defn gen-reported-data [data]
  `(let [data# ~data]
     (or (if (tabellion.config/use-envelope?)
           (if-let [devtools# (cljs.core/aget js/window "devtools")]
             (if-let [toolbox# (cljs.core/aget devtools# "toolbox")]
               (if-let [envelope# (cljs.core/aget toolbox# "envelope")]
                 (if (cljs.core/fn? envelope#)
                   (envelope# data# "details"))))))
         data#)))

(defn gen-console-method [kind]
  (case kind
    :error `(aget js/console "error")
    :warning `(aget js/console "warn")))

(defn gen-report-runtime-message [kind msg data]
  (debug-assert (contains? #{:error :warning} kind))
  (let [mode (case kind
               :error `(tabellion.config/get-error-reporting)
               :warning `(tabellion.config/get-warning-reporting))]
    `(case ~mode
       :throw (throw (tabellion.state/prepare-error-from-call-site ~(gen-reported-message msg) ~(gen-reported-data data)))
       :console ((tabellion.state/get-console-reporter) ~(gen-console-method kind) ~(gen-reported-message msg) ~(gen-reported-data data))
       false nil)))

(defn gen-debug-runtime-state-consistency-check [body]
  (if-not (config/debug?)
    body
    (let [captured-runtime-state-sym (gensym "captured-runtime-state")
          result-sym (gensym "result")]
      ; we don't want body to change *runtime-state* without restoring
      ; this could theoretically happen with code-rewriting when using go-macros
      `(let [~captured-runtime-state-sym tabellion.state/*runtime-state*
             ~result-sym ~body]
         (assert (identical? ~captured-runtime-state-sym tabellion.state/*runtime-state*))
         ~result-sym))))

; this method is hand-written to reduce space (it will be emitted on each macro call-site)
; also to avoid busy-work generated by ClojureScript compiler handling varargs
(def console-reporter-template "function(){arguments[0].apply(console,Array.prototype.slice.call(arguments,1))}")

(defn gen-runtime-context! [& body]
  (let [body-code `(do ~@body)]
    (let [console-reporter (list 'js* console-reporter-template)
          call-site-error `(js/Error.)]
      ; it is imporant to keep console-reporter and call-site-error inline so we get proper call-site location and line number
      `(binding [tabellion.state/*runtime-state* (tabellion.state/prepare-state ~call-site-error ~console-reporter)]
         ~(gen-debug-runtime-state-consistency-check body-code)))))

(defn gen-supress-reporting? [msg-id]
  `(contains? (tabellion.config/get-suppress-reporting) ~msg-id))

(defn should-elide-log-level? [level]
  (debug-assert (contains? constants/all-levels level))
  (contains? (config/elided-log-levels) level))

; -- raw implementations ----------------------------------------------------------------------------------------------------

(defn gen-log-impl [level item-list]
  (debug-assert (integer? level))
  `(tabellion.core/log-dynamically ~level (cljs.core/array ~@item-list)))

; -- shared macro bodies ----------------------------------------------------------------------------------------------------

(defn gen-log [level item-list]
  (if-not (should-elide-log-level? level)
    (gen-runtime-context!
      (gen-log-impl level item-list))))
